package com.pacheco.mee_4;

import java_cup.runtime.*;
import java.io.IOException;
import java.io.File;
import java.io.FileInputStream;
import java.lang.Object;
import java.util.*;
import java.lang.*;
import java_cup.runtime.Symbol;
import java.util.function.Function;
import java.util.stream.Collectors;

parser code {:

    private static final String T_REAL = "real";
    private static final String T_INT = "int";
    private static final String T_STR = "str";
    private static final String T_BOOL = "bool";

    @FunctionalInterface
    public interface AritFunc<T> {
        public T apply(T a, T b);
    }

    class Value {
        public String type;
        public Object value;
        public Value(String t, Object v) {
            type = t;
            value = v;
        }
    }

    protected Lexer lexer;
    private HashMap<String, Value> symT;
    private FileInputStream fis;

    public Parser(String filename) {
        symT = new HashMap<String,Value>();
        File file = new File(filename);
        try {
            fis = new FileInputStream(file);
        } catch (IOException e) {
            e.printStackTrace();
        }
    }

    private void error(String message, Object... args) {
        System.out.println(String.format("ERRO: " + message, args));
        System.exit(1);
    }

    private void print(Object o) {
        System.out.println(o);
    }

    private void print(Value v) {
        System.out.println(valueToString(v));
    }

    private String valueToString(Value v) {
        String result = "";
        if (v.type.equals(T_BOOL)) {
            if ((Boolean) v.value) result = "True";
            else result = "False";
        }
        else {
            result = v.value.toString();
        }
        return result;
    }

    private void print(Value format, List<Value> args) {
        List<String> strArgs = args.stream()
            .map(v -> valueToString(v))
            .collect(Collectors.toList());
        String text = (String)format.value;
        for (String arg : strArgs) {
            text = text.replaceFirst("%", arg);
        }
        System.out.println(text);
    }

    private void register(String type, String name) {
        // defining default values
        Object defaultVal = null;
        if (type.equals(T_BOOL)) defaultVal = false;
        register(type, name, defaultVal);
    }

    // register new variables
    private void register(String type, String name, Object value) {
        symT.put(name, new Value(type, value));
    }

    private Value getValue(String name) {
        if (!symT.containsKey(name)) {
            error("variable '%s' is not declared", name);
        }
        return symT.get(name);
    }

    private int typeValue(String t1) {
        if (t1.equals(T_STR)) return 1000;
        if (t1.equals(T_REAL)) return 999;
        if (t1.equals(T_INT)) return 998;
        if (t1.equals(T_BOOL)) return 997;
        return 9999;
    }

    private String maxType(String t1, String t2) {
        int t1val = typeValue(t1);
        int t2val = typeValue(t2);
        if (t1val > t2val) return t1;
        else return t2;
    }

    private boolean checkAritTypes(String t1, String t2) {
        boolean valid = false;
        if ((t1.equals(T_REAL) || t1.equals(T_INT)) &&
            (t2.equals(T_REAL) || t2.equals(T_INT))) {
                valid = true;
        }
        return valid;
    }

    private boolean checkAtribTypes(String t1, String t2) {
        boolean valid = false;
        if (t1.equals(t2)) valid = true;
        if (t1.equals(T_REAL) && t2.equals(T_INT)) valid = true;
        return valid;
    }

    private boolean checkBoolTypes(String t1, String t2) {
        boolean valid = false;
        if (t1.equals(T_BOOL) && t2.equals(T_BOOL)) valid = true;
        return valid;
    }

    private void atrib(String name, Value newVal) {
        Value oldVal = getValue(name);
        if(!checkAtribTypes(oldVal.type, newVal.type))
            error("cannot convert '%s' to '%s'", newVal.type, oldVal.type);
        symT.put(name, newVal);
    }
    
    // arithmetic operations
    private Value aritOp(Value v1, Value v2, String op, AritFunc<Double> realOp, 
        AritFunc<Integer> intOp) {

        if(!checkAritTypes(v1.type, v2.type))
            error("the operator '%s' connot be applied to '%s' and '%s'", op, v1.type, v2.type);
        String type = maxType(v1.type, v2.type);
        Object value = null;
        if (type.equals(T_REAL)) 
            value = realOp.apply(((Number)v1.value).doubleValue(), ((Number)v2.value).doubleValue());
        else
            value = intOp.apply(((Number)v1.value).intValue(), ((Number)v2.value).intValue());
        return new Value(type, value);
    }

    private Value add(Value v1, Value v2) {
        return aritOp(v1, v2, "+", 
            (a, b) -> (a + b),  // double function
            (a, b) -> (a + b)); // integer function
    }

    private Value subtract(Value v1, Value v2) {
        return aritOp(v1, v2, "-", 
            (a, b) -> (a - b), 
            (a, b) -> (a - b));
    }

    private Value divide(Value v1, Value v2) {
        return aritOp(v1, v2, "/", 
            (a, b) -> (a / b), 
            (a, b) -> (a / b));
    }

    private Value multiply(Value v1, Value v2) {
        return aritOp(v1, v2, "*", 
            (a, b) -> (a * b), 
            (a, b) -> (a * b));
    }

    private Value power(Value v1, Value v2) {
        return aritOp(v1, v2, "**", 
            (a, b) -> (Math.pow(a, b)), 
            (a, b) -> Double.valueOf(Math.pow(a, b)).intValue());
    }

    private Value and(Value v1, Value v2) {
        if (!checkBoolTypes(v1.type, v2.type))
            error("the operator 'and' cannot be applied to '%s' and '%s'", v1.type, v2.type);
        return new Value(T_BOOL, ((Boolean)v1.value) && ((Boolean)v2.value));
    }

    private Value or(Value v1, Value v2) {
        if (!checkBoolTypes(v1.type, v2.type))
            error("the operator 'or' cannot be applied to '%s' and '%s'", v1.type, v2.type);
        return new Value(T_BOOL, ((Boolean)v1.value) || ((Boolean)v2.value));
    }

    private Value not(Value v) {
        if (!v.type.equals(T_BOOL))
            error("the operator 'not' cannot be applied to '%s'", v.type);
        return new Value(T_BOOL, !(Boolean)v.value);
    }

    private boolean checkEqualityTypes(String t1, String t2) {
        // TODO: real must can be compared with int
        return t1.equals(t2);
    }

    private boolean checkCompareTypes(String t1, String t2) {
        boolean valid = false;
        if ((t1.equals(T_REAL) || t1.equals(T_INT)) &&
            (t2.equals(T_REAL) || t2.equals(T_INT))) {
            valid = true;
        }
        return valid;
    }
    
    private Value relOp(Value v1, Value v2, String op) {
        boolean result = false;
        if (op.equals("==") || op.equals("!=")) {
            if (!checkEqualityTypes(v1.type, v2.type))
                error("the operator '%s' cannot not be applied to '%s' and '%s'", op, v1.type, v2.type);
            if (op.equals("==")) result = v1.value == v2.value;
            else result = v1.value != v2.value;
        }
        if (op.equals("<") || op.equals(">") || op.equals("<=") || op.equals(">=")) {
            if (!checkCompareTypes(v1.type, v2.type))
                error("the operator '%s' cannot not be applied to '%s' and '%s'", op, v1.type, v2.type);
            double doubleVal1 = ((Number)v1.value).doubleValue();
            double doubleVal2 = ((Number)v2.value).doubleValue();
            if (op.equals("<")) result = doubleVal1 < doubleVal2;
            if (op.equals(">")) result = doubleVal1 > doubleVal2;
            if (op.equals("<=")) result = doubleVal1 <= doubleVal2;
            if (op.equals(">=")) result = doubleVal1 >= doubleVal2;
        }
        return new Value(T_BOOL, result);
    }
:};

init with {:
    ComplexSymbolFactory f = new ComplexSymbolFactory();
    symbolFactory = f;
    lexer = new Lexer(f,fis);
:};

scan with {: 
    /*Symbol s = lexer.yylex();
    print(s);
    return s;*/
    return lexer.yylex();
:};

terminal Double     REAL;
terminal Integer    INT;
terminal Boolean    BOOL;
terminal String     STR;
terminal String     TYPE;
terminal String     ID;
terminal            ADD;
terminal            MINUS;
terminal            MULT;
terminal            DIV;
terminal            POW;
terminal            OR;
terminal            AND;
terminal            NOT;
terminal String     REL_OP;
terminal            L_BRACKET;
terminal            R_BRACKET;
terminal            SC;
terminal            EQ;
terminal            PRINT;

non terminal        S;
non terminal        COMMANDS;
non terminal        COMMAND;
non terminal        ATRIB;
non terminal Value  NUM;
non terminal Value  EXP;
non terminal Value  BOOLEAN;
non terminal Value  STRING;
non terminal List<Value> PRINT_ARGS;
non terminal        PRINT_CMD;
non terminal        PRINT_PARAMS;
non terminal        DECL;

precedence left OR;
precedence left AND;
precedence left REL_OP;
precedence left ADD, MINUS;
precedence left MULT, DIV;
precedence left POW;

S ::= COMMANDS
    ;

COMMANDS ::= COMMAND COMMANDS
          |
          ;

COMMAND ::= ATRIB SC
          | PRINT_CMD SC
          | DECL SC
          ;

DECL ::= TYPE:t ID:id           {: register(t, id); :};
ATRIB ::= ID:id EQ EXP:e        {: atrib(id, e); :};
PRINT_CMD ::= PRINT PRINT_PARAMS;

PRINT_PARAMS ::= STRING:s PRINT_ARGS:args {: print(s, args); :}
               | EXP:e                    {: print(e); :}
               ;

PRINT_ARGS ::= EXP:e PRINT_ARGS:args {: args.add(e); RESULT = args; :}
             |                       {: RESULT = new ArrayList<Value>(); :}
             ;

EXP ::= NUM:n                   {: RESULT = n; :}
      | ID:id                   {: RESULT = getValue(id); :}
      | BOOLEAN:b               {: RESULT = b; :}
      | L_BRACKET EXP:e R_BRACKET {: RESULT = e; :}
      | EXP:e1 ADD EXP:e2       {: RESULT = add(e1, e2); :}
      | EXP:e1 MINUS EXP:e2     {: RESULT = subtract(e1, e2); :}
      | EXP:e1 DIV EXP:e2       {: RESULT = divide(e1, e2); :}
      | EXP:e1 MULT EXP:e2      {: RESULT = multiply(e1, e2); :}
      | EXP:e1 POW EXP:e2       {: RESULT = power(e1, e2); :}
      | EXP:e1 AND EXP:e2       {: RESULT = and(e1, e2); :}
      | EXP:e1 OR EXP:e2        {: RESULT = or(e1, e2); :}
      | EXP:e1 REL_OP:op EXP:e2 {: RESULT = relOp(e1, e2, op); :}
      | NOT EXP:e               {: RESULT = not(e); :}
      ;

NUM ::= REAL:n          {: RESULT = new Value(T_REAL, n); :}
      | INT:n           {: RESULT = new Value(T_INT, n); :}
      ;

BOOLEAN ::= BOOL:b      {: RESULT = new Value(T_BOOL, b); :};

STRING ::= STR:s        {: RESULT = new Value(T_STR, s); :};